description = 'OSH Vaadin WidgetSet'

configurations {
  vaadin {
    extendsFrom compile
  }
}

dependencies {
  compile 'com.vaadin:vaadin-shared:' + vaadinVersion
  compile 'com.vaadin:vaadin-server:' + vaadinVersion
  compile 'com.vaadin:vaadin-client-compiled:' + vaadinVersion
  compile 'com.vaadin:vaadin-themes:' + vaadinVersion
  vaadin 'com.vaadin:vaadin-client-compiler:' + vaadinVersion 
}

task compileWidgetset(type: JavaExec) {
  main = 'com.vaadin.tools.WidgetsetCompiler'
  classpath sourceSets.main.resources.srcDirs
  classpath configurations.vaadin
  jvmArgs '-Xmx512M', '-Xss512M', '-Dgwt.persistentunitcachedir='+project.buildDir.canonicalPath
  args '-war', "$projectDir/src/main/resources/VAADIN/widgetsets"
  args '-deploy', "$projectDir/build/deploy"
  args '-optimize', '0'
  args '-localWorkers', '' + Runtime.getRuntime().availableProcessors()
  args '-logLevel', 'WARN'
  args 'org.sensorhub.ui.SensorHubWidgetSet'
  /*doFirst {
    println commandLine
  }*/
}

task compileTheme(type: JavaExec) {
  main = 'com.vaadin.sass.SassCompiler'
  classpath sourceSets.main.resources.srcDirs
  classpath configurations.vaadin
  jvmArgs '-Xmx64M', '-Xss64M'
  args "$projectDir/src/main/resources/VAADIN/themes/sensorhub/styles.scss"
  args "$projectDir/src/main/resources/VAADIN/themes/sensorhub/styles.css"
  /*doFirst {
    println commandLine
  }*/
}

task cleanVaadin(type: Delete) {
  delete fileTree("$projectDir/src/main/resources/VAADIN/themes") {
      include '**/*.css'
  }
  delete "$projectDir/src/main/resources/VAADIN/widgetsets"
}

processResources.dependsOn compileWidgetset
processResources.dependsOn compileTheme
clean.dependsOn cleanVaadin